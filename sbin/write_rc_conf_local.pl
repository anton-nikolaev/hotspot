#!/usr/bin/perl

# This script generates vlan and ifconfig variables for rc.conf.
# Usage: ./this_script.pl > /etc/rc.conf.local

# There should be in the rc.conf file:  
# rc_conf_files="/etc/rc.conf /etc/rc.conf.local"

use strict;
use Config::IniFiles;
use Net::CIDR;
use DBM::Deep;
use Data::Dumper;
use constant CONFIG_PATH => '/usr/local/etc/hotspot.ini';

my $ini = Config::IniFiles->new(-file => CONFIG_PATH)
	or die "Cant read ini file ".CONFIG_PATH;
my $debug = $ini->val('GLOBAL', 'debug');

sub warn_debug
{
	my $msg = shift;
	return 0 unless $debug eq 'yes';
	return warn "DEBUG $msg";
}

my @ifconfig_lines;
my %vlans;
foreach my $iface ($ini->Sections)
{
	next if $iface eq 'GLOBAL';

	my $my_ip = $ini->val($iface, 'my_ip_address');
	$my_ip = Net::CIDR::cidrvalidate($my_ip);
	unless ($my_ip)
	{
		warn "Invalid my_ip_address parameter in config section $iface";
		next;
	};

	my $hs_net = $ini->val($iface, 'hs_net');
	$hs_net = Net::CIDR::cidrvalidate($hs_net);
	unless ($hs_net)
	{
		warn "Invalid hs_net parameter in config section $iface";
		next;
	};
	
	unless ($hs_net =~ /\/(\d+)$/)
	{
		warn "Cant get prefix from hs_net parameter in config section $iface";
		next;
	};
	my $net_prefix = $1;

	$my_ip =~ s/\/\d{1,2}$/\/$net_prefix/;
	
	if ($ini->val($iface, 'create_vlan'))
	{
		unless ($iface =~ /^(\w+\d+)\.(\d+)$/)
		{
			warn "Invalid interface: $iface";
		}
		else
		{
			my $dev = $1;
			my $vlan_id = $2;
			if (exists $vlans{$dev})
			{
				push (@{$vlans{$dev}}, $vlan_id);
			}
			else
			{
				$vlans{$dev} = [ $vlan_id ];
			};
			push (@ifconfig_lines, $dev.'_'.$vlan_id.'="inet '.$my_ip.'"');
		};
	}
	else
	{
		push (@ifconfig_lines, $iface.'="inet '.$my_ip.'"');
	};
	
};

print STDOUT join 
	("\n",
		"# rc.conf variables for burnet hotspot",
		"# DO NOT EDIT THIS FILE MANUALLY!!!!",
		"# it is automatically regenerated",
		"\n"
	);

foreach (keys %vlans)
{
	print STDOUT "vlans_$_=\"".join(" ", @{$vlans{$_}})."\"\n";
};

foreach (@ifconfig_lines)
{
	print STDOUT "ifconfig_$_\n";
};

#warn Dumper \%vlans;
#warn Dumper \@ifconfig_lines;
